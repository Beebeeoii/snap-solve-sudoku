apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'com.google.gms.google-services'

android {

    compileSdkVersion 33

    defaultConfig {
        applicationId "com.beebeeoii.snapsolvesudoku"
        minSdkVersion 26
        targetSdkVersion 33
        versionCode 35
        versionName "2.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    splits {
        abi {
            enable true
            reset()
            include 'x86', 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'mips', 'mips64', 'x86_64'
            universalApk true
        }
    }
    androidResources {
        noCompress '070820090621'
    }
    namespace 'com.beebeeoii.snapsolvesudoku'

    project.ext.versionCodes = ['armeabi': 1, 'armeabi-v7a': 2, 'arm64-v8a': 3, 'mips': 5, 'mips64': 6, 'x86': 8, 'x86_64': 9]

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(
                            com.android.build.OutputFile.ABI), 0) * 10000000 + android.defaultConfig.versionCode
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation project(path: ':opencv455')

    implementation 'org.tensorflow:tensorflow-lite:2.2.0'
    implementation 'org.tensorflow:tensorflow-lite-support:0.1.0-rc1'
    implementation 'org.tensorflow:tensorflow-lite-gpu:2.2.0'

    implementation 'com.google.android.material:material:1.6.1'

    def nav_version = "2.4.2"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation 'com.theartofdev.edmodo:android-image-cropper:2.8.0'

    def room_version = "2.4.2"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    def preference_version="1.2.0"
    implementation "androidx.preference:preference-ktx:$preference_version"

    implementation 'com.heinrichreimersoftware:android-issue-reporter:1.4'

    implementation 'com.thefinestartist:finestwebview:1.2.7'

    def latestAboutLibsRelease = "8.3.0"
    implementation "com.mikepenz:aboutlibraries-core:$latestAboutLibsRelease"
    implementation "com.mikepenz:aboutlibraries:$latestAboutLibsRelease"

    def changelogVersion = "1.1.7"
    implementation "com.github.MFlisar:changelog:$changelogVersion"

    implementation 'com.anjlab.android.iab.v3:library:1.1.0'

    //firestore
    implementation 'com.google.firebase:firebase-firestore-ktx:24.1.2'
    implementation 'com.google.firebase:firebase-storage-ktx:20.0.1'

    implementation 'com.karumi:dexter:6.2.1'

    implementation 'com.github.hosseiniSeyRo:android-app-rating:1.1.2'

    implementation "androidx.window:window:1.1.0-alpha02"
}
